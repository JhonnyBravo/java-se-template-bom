plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'eclipse'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'maven-publish'
}

repositories {
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
    jcenter()
    maven {url 'https://jitpack.io'}
}

dependencyManagement {
    dependencies {
        dependency 'gnu.getopt:java-getopt:1.0.13'
        dependency 'ch.qos.logback:logback-classic:1.4.8'
        dependency 'org.hibernate.validator:hibernate-validator:8.0.0.Final'
        dependency 'org.glassfish:jakarta.el:4.0.2'
        dependency 'junit:junit:4.13.2'
    }
}


group='com.github.JhonnyBravo'
version='master-SNAPSHOT'
sourceCompatibility=17
targetCompatibility=17

publishing{
    publications{
        mavenJava(MavenPublication){
            artifactId='java-se-template-bom'
            versionMapping{
                usage('java-api'){
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime'){
                    fromResolutionResult()
                }
            }
            pom{
                name='java-se-template-bom'
                description='Java SE プロジェクト用の BOM プロジェクト'
                url='https://github.com/JhonnyBravo/java-se-template-bom'
            }
        }
    }
    repositories{
        maven{
            def releasesRepoUrl=layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl=layout.buildDirectory.dir('repos/snapshots')
            url=version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
        maven{
            name='GitHubPackages'
            url=uri('https://maven.pkg.github.com/jhonnybravo/java-se-template-bom')
            credentials{
                username=project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                password=project.findProperty('gpr.key') ?: System.getenv('TOKEN')
            }
        }
    }
}
